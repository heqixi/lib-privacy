apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    getArchiveClassifier().set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifactoryPublish.dependsOn('build')

publishing {
    publications {
        aar(MavenPublication) {
            groupId POM_GROUP
            version POM_VERSION_NAME + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
            artifactId POM_ARTIFACT_ID
            artifact "$buildDir/outputs/aar/${project.name}-release.aar"
            artifact androidSourcesJar
            artifact androidJavadocsJar
        }
    }
}

artifactory {
    contextUrl = "https://artifactory.gz.cvte.cn:443/artifactory"
    publish {
        repository {
            repoKey = Boolean.valueOf(System.getProperty("snapshot")) ? "SR_maven_snapshots_local" : "SR_maven_releases_local"
            username = System.getProperty("username")
            password = System.getProperty("password")
        }
        defaults {
            publications('aar')
        }
    }
}

artifacts {
    archives androidJavadocsJar
    archives androidSourcesJar
}
